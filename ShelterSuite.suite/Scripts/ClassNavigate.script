//Generic device-level navigation methods

to UnlockDevice
	if(ImageFound("Device/System/LockIcon")) is not "false"
		Drag "Device/System/LockIcon"
		wait 1 second
		Drop (460,635)
		//Drop "Device/System/Unlockicon" [mechanism unstable]
		wait 1 second
	end if
end UnlockDevice

to ResetState
	run ClassNavigate.UnlockDevice
	Click "Device/System/HomeButton"
	wait 1 second
	Click "Device/System/HomeButton"
	wait 1 second
	run ClassUtilities.ConfirmImageOnScreen("Device/System/AppsIcon")
	wait 1 second
end ResetState

to OpenTaskBar
	Drag (240,25)
	Drop (240,424)
end OpenTaskBar

to CloseTaskBar
	Drag (240,790)
	Drop (240,424)
end CloseTaskBar

to SwipeLeft
	Drag (460,125)
	Drop (260,125)
end SwipeLeft

to SwipeRight
	Drag (100,125)
	Drop (300,125)
end SwipeRight

to SwipeUp
	Drag (465,500)
	Drop (465,340)
end SwipeUp

to SwipeDown
	Drag (465,340)
	Drop (465,500)
end SwipeDown

to OpenApp name
	Click "Device/System/AppsIcon"
	repeat until (ImageFound(3,Text:name)) is "true" //"3" within ImageFound sets total wait time to 3 secs
		run ClassNavigate.SwipeLeft
	end repeat
	Click (Text:name)
end OpenApp

to SettingsOption name
	repeat until ImageFound(Text:name) is "true"
		repeat until ImageFound(3,Text:WIRELESS) is "true"
			ScrollWheelUp 10
		end repeat
		wait 1 second
		repeat until ImageFound(3,Text:name) is "true"
			ScrollWheelDown 1
		end repeat
		wait 1 second
	end repeat
	Click (Text:name)
	wait 1 second
end SettingsOption

to GoToBrowserAddress address
	Click "Device/System/MenuButton"
	Click (Text:"Find on page")
	Click "Device/System/BackIcon"
	Click "Device/System/BackIcon"
	wait 2 second
	Click (240,50)
	TypeText (address)
	Click "Device/Apps/Browser/GoButton"
	wait 5 seconds
end GoToBrowserAddress

to GoHome
	if(ImageFound("Device/System/AppsIcon")) is not "false"
		Click "Device/System/HomeButton"
	end if
end GoHome

to AnswerCall
	if(ImageFound("Device/Apps/Telephone/IncomingCall"))
		Drag "Device/Apps/Telephone/IncomingCall"
		wait 1 second
		Drop (460,635)
		//Drop "Device/System/Unlockicon" [mechanism unstable]
		wait 1 second
	end if
end AnswerCall