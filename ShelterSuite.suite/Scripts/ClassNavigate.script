//Generic device-level navigation methods

to UnlockDevice
	if(ImageFound("Device/System/LockIcon")) is not "false"
		Log "Device is locked.  Now unlocking..."
		Drag "Device/System/LockIcon"
		wait 1 second
		Drop (460,635)
		//Drop "Device/System/Unlockicon" [mechanism unstable]
		wait 1 second
	end if
end UnlockDevice

to ResetState
	if ImageFound(3, "Device/System/AppsIcon") = "true"
		wait 1 second
		Log "Device is on home screen."
	else
		if ImageFound(3, Text:"OK", CaseSensitive: true) = "true"
			Click (Text:"OK")
			Log "Android warning popup detected.  OK selected."
		end if
		Log "Returning to home screen."
		run ClassNavigate.UnlockDevice
		Click "Device/System/HomeButton"
		wait 1 second
		Click "Device/System/HomeButton"
		wait 1 second
		run ClassUtilities.ConfirmImageOnScreen("Device/System/AppsIcon")
		wait 1 second
	end if
end ResetState

to OpenTaskBar
	Log "Opening taskbar."
	Drag (240,25)
	Drop (240,424)
	wait 1 second
	run ClassUtilities.ConfirmImageOnScreen("Device/System/Taskbar/TaskbarDropdownBar")
end OpenTaskBar

to CloseTaskBar
	Log "Closing taskbar."
	Drag (240,790)
	Drop (240,424)
	run ClassUtilities.ConfirmImageNotOnScreen("Device/System/Taskbar/TaskbarDropdownBar")
end CloseTaskBar

to SwipeLeft
	Log "Swiping left."
	Drag (460,125)
	Drop (260,125)
end SwipeLeft

to SwipeRight
	Log "Swiping right."
	Drag (100,125)
	Drop (300,125)
end SwipeRight

to SwipeUp
	Log "Swiping up."
	Drag (465,500)
	Drop (465,340)
end SwipeUp

to SwipeDown
	Log "Swiping down."
	Drag (465,340)
	Drop (465,500)
end SwipeDown

to OpenApp name
	Log "Opening apps menu."
	Click "Device/System/AppsIcon"
	repeat until (ImageFound(3,Text:"Browser")) is "true" //"3" within ImageFound sets total wait time to 3 secs
		run ClassNavigate.SwipeRight
		wait 1 second
	end repeat
	wait 1 second
	Log "Locating " &name& " app."
	repeat until (ImageFound(3,Text:name)) is "true"
		run ClassNavigate.SwipeLeft
	end repeat
	Log "Opening" &name& "app."
	Click (Text:name)
	wait 1 second
end OpenApp

to SettingsOption name
	Log "Beginning at top of Options List."
	ScrollWheelUp 10
	wait 2 seconds
	repeat until ImageFound(Text:name) is "true"
		repeat until ImageFound(Text:WIRELESS) is "true"
			Log "Returning to top of Options List."
			ScrollWheelUp 10
			wait 2 seconds
		end repeat
		wait 1 second
		repeat until ImageFound(Text:name) is "true"
			Log "Searching for " &name& " Settings Option."
			ScrollWheelDown 1
			wait 2 seconds
		end repeat
		wait 1 second
	end repeat
	Log "Settings Option " &value& " Found."
	Click (Text:name)
	wait 1 second
end SettingsOption

to GoToBrowserAddress address
	Log "Going to website " &address& "."
	Click "Device/System/MenuButton"
	wait 1 second
	Click (Text:"Find on page", SearchRectangle:((40,100),(440,800)))
	wait 1 second
	Click "Device/System/BackIcon"
	Click "Device/System/BackIcon"
	wait 2 second
	Click (240,50)
	TypeText (address)
	wait 1 second
	Click "Device/Apps/Browser/GoButton"
	wait 5 seconds
end GoToBrowserAddress

to GoHome
	if(ImageFound("Device/System/AppsIcon")) is not "false"
		Click "Device/System/HomeButton"
	end if
end GoHome

to AnswerCall
	Log "Answering phone call."
	if(ImageFound("Device/Apps/Telephone/IncomingCall"))
		Drag "Device/Apps/Telephone/IncomingCall"
		wait 1 second
		Drop (460,635)
		//Drop "Device/System/Unlockicon" [mechanism unstable]
		wait 1 second
	end if
end AnswerCall